BACKUP ~Ehlastra/backup~
AUTHOR ~Roxanne - https://baldursextendedworld.com/Tamoko/ or roxanne@baldursextendedworld.com~

VERSION ~v0.3~
AUTO_TRA ~Ehlastra/Tra/%s~

LANGUAGE ~English~ ~english~ ~Ehlastra/Tra/Setup-Ehlastra.tra~

BEGIN ~Ehlastra ToB~
REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @002

COPY ~Ehlastra/Misc/Ehlastra.bmp~ ~override~

COPY_EXISTING ~HGSLV01.cre~ ~override~
WRITE_ASCII 0x34 ~Ehlastra~ #8
WRITE_ASCII 0x3c ~Ehlastra~ #8
WRITE_ASCII 0x248 ~HG_Ehla~ #8
SAY BIO @005
LAUNCH_PATCH_FUNCTION ADD_CRE_EFFECT
    INT_VAR
    opcode = 360 //Ignore reputation
    timing = 9 //Permanent
    target = 1 //Self
    resist_dispel = 0 //Not dispellable + bypass resistance
  END

COMPILE ~Ehlastra/baf/HG_Ehla.baf~
COMPILE ~Ehlastra/baf/EhlastD.baf~

COMPILE ~Ehlastra/dialogue~

APPEND ~pdialog.2da~
~HGSLV01 *** *** *** EhlastP EhlastJ EhlastD~
UNLESS ~HGSLV01~

APPEND ~interdia.2da~
~HGSLV01 *** EhlastJ ~
UNLESS ~HGSLV01~

STRING_SET
65990 @003
66041 @004

COPY ~Ehlastra/Misc/HG_Axe.itm~ ~override~
SAY NAME1 @006
SAY NAME2 @007
SAY DESC @008
SAY 0x2be Ehlastra

COPY ~Ehlastra/Misc/HG_Axe.bam~ ~override~

COPY ~Ehlastra/Misc//HG_Chan.SPL~ ~override~
     SAY 0xc  @009
     SAY 0x8  @009
     SAY 0x50 @010

EXTEND_TOP ~ar5204.bcs~ ~Ehlastra/baf/HG_5204.baf~
EXTEND_TOP ~ar5202.bcs~ ~Ehlastra/baf/HG_5202.baf~
EXTEND_TOP ~yaga01.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~finmel01.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~abaz2.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~sendai.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~finyaga.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~finabaz.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~finbodh.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~fingrom.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~finiren.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~finsend.bcs~ ~Ehlastra/baf/HG_Axe5.baf~
EXTEND_TOP ~ar6200.bcs~ ~Ehlastra/baf/HG_6200.baf~

COPY ~Ehlastra/Misc/HG_balt.2da~ ~override/HG_balt.2da~ REPLACE 82760 @011
COPY ~Ehlastra/Misc/HG_balt.2da~ ~override/HG_Noba.2da~ REPLACE 82760 @012

ACTION_IF FILE_EXISTS_IN_GAME ~o#bran.cre~ THEN BEGIN
EXTEND_BOTTOM ~ar4000.bcs~ ~Ehlastra/mod/HG_4500.baf~
END

ACTION_IF FILE_EXISTS_IN_GAME ~dw#melis.cre~ THEN BEGIN
	OUTER_SET actual_confront_state = (STATE_WHICH_SAYS 0 IN ~wheels/tra/%s/balthadd.tra~ FROM ~balth~)
COPY ~Ehlastra/Mod/WheelsInterject.d~  ~Ehlastra/Mod/WheelsInterject.d~
REPLACE_TEXTUALLY ~1113~ ~%actual_confront_state%~
COMPILE ~Ehlastra/Mod/WheelsInterject.d~
END

